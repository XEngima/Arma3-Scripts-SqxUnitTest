/*
 * Name:	Assert
 * Date:	2018-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Asserts different kinds of checks.
 */
namespace Sqx.UnitTest
{
	public class Assert
	{
		public static method AreEqual("_expected" as Any, "_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String, "_nilParameter" as String];
			
			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_expected" || isNil "_actual") then {
				if (isNil "_expected") then {
					_nilParameter = "Expected ";
				}
				else {
					_nilParameter = "Actual ";
				};
				
				_fullMsg = _nilParameter + "value is nil. ";
			}
			else {
				if (!(_expected isEqualTo _actual)) then {
					_fullMsg = "Expected '" + str _expected + "' but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method AreNotEqual("_expected" as Any, "_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String, "_nilParameter" as String];

			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_expected" || isNil "_actual") then {
				if (isNil "_expected") then {
					_nilParameter = "Expected ";
				}
				else {
					_nilParameter = "Actual ";
				};
				
				_fullMsg = _nilParameter + "value is nil. ";
			}
			else {
				if (_expected isEqualTo _actual) then {
					_fullMsg = "Expected '" + str _expected + "' but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsTrue("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_actual") then {
				_fullMsg = "Actual value is nil. ";
			}
			else {
				if (!_actual) then {
					_fullMsg = "Expected 'true' but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsFalse("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_actual") then {
				_fullMsg = "Actual value is nil. ";
			}
			else {
				if (_actual) then {
					_fullMsg = "Expected 'false' but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		private static method ValueIsNull("_value" as Any)
		{
			private ["_valueSqxClass" as Assert, "_isNull" as Boolean];

			call TestEngine.SetAssertDone;
			_valueSqxClass = _value as Assert;
			_isNull = false;
			
			if (_value is Object || _value is Control || _value is Display || _value is Group || _value is Location || _value is Task || _value is Config) then {
				if (isNull _value) then {
					_isNull = true;
				};
			};
			
			if (!_isNull && { isNull _valueSqxClass }) then {
				_isNull = true;
			};
				
			_isNull
		};
		
		public static method IsNull("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];
			
			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_actual") then {
				_fullMsg = "Actual value is nil. ";
			}
			else {
				if (!([_actual] call _self.ValueIsNull)) then {
					_fullMsg = "Expected NULL but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsNotNull("_actual" as Any, ["_message" as String, ""]) {
			private ["_fullMsg" as String];
			
			call TestEngine.SetAssertDone;
			_fullMsg = "";

			if (isNil "_actual") then {
				_fullMsg = "Actual value is nil. ";
			}
			else {
				if ([_actual] call _self.ValueIsNull) then {
					_fullMsg = "Expected NOT NULL but got '" + str _actual + "'. ";
					
					if (_message != "") then {
						_fullMsg = _fullMsg + _message;
					};
				};
			};
			
			if (_fullMsg != "") then {
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
	};
};
